/**
 * Rules for the Heroes mode
 */

#Extends "Modes/ShootMania/ModeSport.Script.txt"

#Const Version		"2014-07-24"
#Const ScriptName	"Heroes.Script.txt"

#Setting S_TurnWin				10	as _("Number of points to win a map")				///< Score to reach to win a map
#Setting S_TimePoleElimination	10.	as _("Capture time limit after defense elimination")///< Countdown to capture after defense elimination

#Const C_AtkRocketAmmoMax	4	///< Atk Rocket ammo max
#Const C_AtkRocketAmmoGain	1.	///< Atk Rocket ammo gain
#Const C_AtkLaserAmmoMax	1	///< Atk Laser ammo max
#Const C_AtkLaserAmmoGain	0.8	///< Atk Laser ammo gain
#Const C_AtkNucleusAmmoMax	1	///< Atk Nucleus ammo max
#Const C_AtkNucleusAmmoGain	1.	///< Atk Nucleus ammo gain

#Const C_DefRocketAmmoMax	4	///< Def Rocket ammo max
#Const C_DefRocketAmmoGain	1.	///< Def Rocket ammo gain
#Const C_DefLaserAmmoMax	1	///< Def Laser ammo max
#Const C_DefLaserAmmoGain	1.	///< Def Laser ammo gain
#Const C_DefNucleusAmmoMax	1	///< Def Nucleus ammo max
#Const C_DefNucleusAmmoGain	1.	///< Def Nucleus ammo gain

#Const C_AtkEliminationPoints	1	///< Points for eliminating the attacker

#Const C_RequiredPlayersNb		5	///< Required number of players in each team
#Const S_NbPlayersPerTeamMax	5

#Const Description _("TYPE: Team versus Team (5 vs 5)\nOBJECTIVE:\nOne team is in attack with four players with the Laser and one player with the Nucleus. The other team is in defense with five players with the Rocket.\nThe attackers must capture the pole but only the Nucleus can do it. The defenders must eliminate the Nucleus or prevent the capture.\nThe first team to reach the points limit wins the map.")

***LogVersion***
***
MB_LogVersion(ScriptName, Version);
***

***Rules***
***
/* -------------------------------------- */
// Create rules
declare ModeName = "Heroes";
declare ModeObjectives = TextLib::Compose(_("$<%11. $>The attacker must capture a pole after %2 seconds and before %3 seconds.\n$<%12. $>The defenders must prevent the attacker from capturing a pole.\n$<%13. $>The game is divided in rounds of two turns each.\n$<%14. $>During a round each team play one turn in attack and one turn in defense.\n$<%15. $>The team who wins the turn scores 1 point.\n$<%16. $>The first team to score at least %4 points with %5 ahead of their opponent wins the map."), "$"^SpawnScreen::GetModeColor(), TextLib::ToText(S_TimeLimit - S_TimePole), TextLib::ToText(S_TimeLimit), TextLib::ToText(S_TurnWin), TextLib::ToText(S_TurnGap));
declare ModeConditions = TextLib::Compose(_("$<%11. $>The team in defense play with Rockets.\n$<%12. $>The team in attack play with 4 Lasers and 1 Nucleus.\n$<%13. $>The player with the Nucleus is the attacker and is the only one who can capture a pole.\n$<%14. $>If the attacker is eliminated, his team lose the turn."), "$"^SpawnScreen::GetModeColor());

SpawnScreen::AddSubsection(_("Type"), _("Team versus Team"), 0.);
SpawnScreen::AddSubsection(
	_("Objectives"), 
	ModeObjectives, 
	25.
);
SpawnScreen::AddSubsection(
	_("Conditions"), 
	ModeConditions, 
	85.
);
SpawnScreen::CreatePrettyRules(ModeName);

ModeStatusMessage = _("TYPE: Team versus Team (5 vs 5)\nOBJECTIVE: Capture the pole when you're the attacker. Defend the pole when you're a defender.");
***

***StartServer***
***
ST2::SetStyle("LibST_SMBaseTeams");
ST2::SetStyle("LibST_SMWithLegends");
ST2::CreateCol("DefPoints", _("|Substantive|Hit"), "0", 3., 100.);
ST2::SetColTextAlign("DefPoints", CMlControl::AlignHorizontal::Right);
ST2::CreateCol("AtkPoints", _("|Attack,Substantive|Atk"), "0", 3., 110.);
ST2::SetColTextAlign("AtkPoints", CMlControl::AlignHorizontal::Right);
ST2::SetColTextSize("AtkPoints", 3.);
ST2::Build("SM");
***

***StartMap***
***
ST2::ClearScores();
ST2::Attach();
***

***InitTurn***
***
declare ElimationCountdown = False;	///< Switch the elimination countdown for capture on or off
declare LastDefSelfElim = False;	///< True if the last defenders respawned/fell in an offzone
***

***SpawnDef***
***
if (DefSpawned >= MaxDef) break;
if (!Players.existskey(PlayerId)) continue;

declare Player <=> Players[PlayerId];
declare CurrentWeapon for Player = 1;
CurrentWeapon = 1;
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Rocket, C_DefRocketAmmoMax);
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Laser, C_DefLaserAmmoMax);
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Nucleus, C_DefNucleusAmmoMax);

Player.AmmoGain		= 1. * C_DefRocketAmmoGain;
Player.StaminaMax	= 1. * C_DefStaminaMaxMultiplier;
Player.StaminaGain	= 1. * C_DefStaminaMaxMultiplier;
Player.ArmorMax		= C_DefArmorMax;
SetPlayerWeapon(Player, CSmMode::EWeapon::Rocket, True); 
SM::SpawnPlayer(Player, G_DefClan, DefSpawn, Now);
Player.IsHighlighted = True;
DefSpawned += 1;
***

***SpawnAtk***
***
if (AtkSpawned >= C_RequiredPlayersNb) break;
if (!Players.existskey(PlayerId)) continue;
			
declare Player <=> Players[PlayerId];
declare CurrentWeapon for Player = 1;
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Rocket, C_AtkRocketAmmoMax);
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Laser, C_AtkLaserAmmoMax);
SetPlayerAmmoMax(Player, CSmMode::EWeapon::Nucleus, C_AtkNucleusAmmoMax);

Player.StaminaMax	= 1.;
Player.StaminaGain	= 1.;
Player.ArmorMax		= C_DefArmorMax;
if (Player.Id == G_AtkPlayerId) {
	CurrentWeapon = 3;
	Player.AmmoGain		= 1. * C_AtkNucleusAmmoGain;
	SetPlayerWeapon(Player, CSmMode::EWeapon::Nucleus, False);
} else {
	CurrentWeapon = 2;
	Player.AmmoGain		= 1. * C_AtkLaserAmmoGain;
	SetPlayerWeapon(Player, CSmMode::EWeapon::Laser, False);
}
SM::SpawnPlayer(Player, G_AtkClan, AtkSpawn, Now);
Player.IsHighlighted = True;
AtkSpawned += 1;
***

***PoleManagement***
***
/* -------------------------------------- */
// All defenders eliminated, start the capture countdown
if (ClansNbPlayersAlive[G_DefClan] <= 0 && S_TimePoleElimination >= 0) {
	if (!ElimationCountdown) {
		ElimationCountdown = True;
		UIManager.UIAll.BigMessageSound = CUIConfig::EUISound::PhaseChange;
		UIManager.UIAll.BigMessageSoundVariant = 0;
		UIManager.UIAll.BigMessage = _("All defenders eliminated, capture the goal!");
		UIManager.UIAll.CountdownEndTime = -1;
		EndTime = Now + MathLib::NearestInteger(S_TimePoleElimination * 1000);
	}
	foreach (LandmarkGauge in MapLandmarks_Gauge) {
		if (LandmarkGauge.Gauge.Clan == G_DefClan) {
			LandmarkGauge.Gauge.Clan = G_AtkClan;
			LandmarkGauge.Gauge.Value = 0;
			LandmarkGauge.Gauge.Max = MathLib::NearestInteger(S_TimeCapture * 1000) * MapLandmarks_Gauge.count;
		}
		
		if (LandmarkGauge.Sector != Null && LandmarkGauge.Sector.PlayersIds.exists(G_AtkPlayerId)) {
			LandmarkGauge.Gauge.Speed = 1;
		} else {
			LandmarkGauge.Gauge.Speed = 0;
		}
	}
} else if (Now >= PoleTime) {
	if (!GoalCanBeCaptured) {
		GoalCanBeCaptured = True;
		UIManager.UIAll.BigMessageSound = CUIConfig::EUISound::PhaseChange;
		UIManager.UIAll.BigMessageSoundVariant = 0;
		UIManager.UIAll.BigMessage = _("The goal can now be captured.");
		UIManager.UIAll.CountdownEndTime = -1;
	}
	
	/* -------------------------------------- */
	// Check if the attacker is capturing the pole
	foreach (LandmarkGauge in MapLandmarks_Gauge) {
		if (LandmarkGauge.Gauge.Value == 0 && LandmarkGauge.Gauge.Clan == G_DefClan) {
			LandmarkGauge.Gauge.Clan = G_AtkClan;
			LandmarkGauge.Gauge.Max = MathLib::NearestInteger(S_TimeCapture * 1000) * MapLandmarks_Gauge.count;
		}
		
		if (LandmarkGauge.Sector != Null && LandmarkGauge.Sector.PlayersIds.exists(G_AtkPlayerId)) {
			if(LandmarkGauge.Gauge.Clan == G_DefClan) {
				LandmarkGauge.Gauge.Speed = -1;
			} else {
				LandmarkGauge.Gauge.Speed = 1;
			}
		} else {
			LandmarkGauge.Gauge.Speed = 0;
		}
	}
}
***

***EventOnHit***
***
/* -------------------------------------- */
// Friendly fire off
if (Event.Shooter.CurrentClan == Event.Victim.CurrentClan) {
	Discard(Event);
} else {
	Event.Damage = 100;
	/* -------------------------------------- */
	// Shooter is in atk clan, victim in def clan
	if (Players.existskey(G_AtkPlayerId)) {
		if (Event.Shooter.CurrentClan == G_AtkClan) {
			if (LastActionClanNb != G_AtkClan) SoundVariant += 1;
			LastActionClanNb = G_AtkClan;
			
			// Elim last defender
			if (ClansNbPlayersAlive[G_DefClan] <= 1) {
				PlaySound(CUIConfig::EUISound::VictoryPoint, 0);
			} 
			// Elim standard defender
			else {
				PlaySound(CUIConfig::EUISound::TieBreakPoint, 4);
			}
			
			if (ClansNbPlayersAlive[G_DefClan] > 1) {
				declare DefLeft = ClansNbPlayersAlive[G_DefClan] - 1;
				if (DefLeft <= 1) {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						_("%1 1 defender left."), 
						Teams[G_DefClan - 1].ColorText
					);
				} else {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						_("%1 %2 defenders left."), 
						Teams[G_DefClan - 1].ColorText, 
						TextLib::ToText(DefLeft)
					);
				}								
				LastStatusTime = Now;
			}
			
			if (Event.Shooter.Score != Null) {
				declare NbHit for Event.Shooter.Score = 0;
				NbHit += 1;
				declare netwrite Net_LayerST_DefPoints for Event.Shooter = 0;
				Net_LayerST_DefPoints = NbHit;
				ST2::SetColValue("DefPoints", Event.Shooter.Score, TextLib::ToText(NbHit));
			}
			
			if (Event.WeaponNum == GetWeaponNum(CSmMode::EWeapon::Laser)) DisplayHitDistance(Event.Shooter, Event.Victim, False);
		} 
		/* -------------------------------------- */
		// Shooter is in def clan, victim in atk clan
		else {
			if (LastActionClanNb != G_DefClan) SoundVariant += 1;
			LastActionClanNb = G_DefClan;
			
			// Elim nucleus attacker
			if (Event.Victim.Id == G_AtkPlayerId) {
				PlaySound(CUIConfig::EUISound::VictoryPoint, 0);
			} 
			// Elim standard attacker
			else {
				PlaySound(CUIConfig::EUISound::TieBreakPoint, 0);
			}
			
			declare NbLeft = 0;
			if (Event.Victim.Id == G_AtkPlayerId) {
				NbLeft = (Players[G_AtkPlayerId].Armor - 100) / 100;
				if (NbLeft <= 1) {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						"%1%2", Teams[G_AtkClan - 1].ColorText, _("1 armor left.")
					);
				} else {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						_("%1 %2 armors left."), 
						Teams[G_AtkClan - 1].ColorText, 
						TextLib::ToText(NbLeft)
					);
				}
			} else {
				NbLeft = ClansNbPlayersAlive[G_AtkClan] - 1;
				if (NbLeft <= 1) {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						"%1%2", Teams[G_AtkClan - 1].ColorText, _("1 attacker left.")
					);
				} else {
					UIManager.UIAll.StatusMessage = TextLib::Compose(
						_("%1 %2 attackers left."), 
						Teams[G_AtkClan - 1].ColorText, TextLib::ToText(NbLeft)
					);
				}
			}
			LastStatusTime = Now;
			
			if (Event.Shooter.Score != Null) {
				declare NbHit for Event.Shooter.Score = 0;
				declare netwrite Net_LayerST_DefPoints for Event.Shooter = 0;
				if (Event.Victim.Id == G_AtkPlayerId) {
					NbHit += C_AtkEliminationPoints;
					Event.ShooterPoints = C_AtkEliminationPoints;
					Net_LayerST_DefPoints = NbHit;
				} else {
					NbHit += 1;
					Net_LayerST_DefPoints = NbHit;
				}
				ST2::SetColValue("DefPoints", Event.Shooter.Score, TextLib::ToText(NbHit));
			}
		}
	}
	
	if (Event.WeaponNum == 1) {
		DisplayHitDistance(Event.Shooter, Event.Victim, True);
	}
	//Layers::Update("ScoresTable", CreateLayerScoresTable());
	XmlRpc::OnHit(Event);
	PassOn(Event);
}
***

***EventOnArmorEmpty***
***
if (Event.Shooter != Null && Event.Shooter.CurrentClan == G_AtkClan) {
	G_MatchDefElim[Event.Shooter.CurrentClan] += 1;
	G_MapDefElim[Event.Shooter.CurrentClan] += 1;
}
// If the last defenders eliminate himself the attackers win
if (ClansNbPlayersAlive[G_DefClan] <= 1
	&& Event.Victim != Null 
	&& Event.Victim.CurrentClan == G_DefClan
	&& Event.Shooter == Null) 
{
	LastDefSelfElim = True;
}

if (Event.Victim.Id == G_AtkPlayerId && Event.Shooter != Null) G_AtkElimPlayerId = Event.Shooter.Id;
XmlRpc::OnArmorEmpty(Event);
PassOn(Event);
***

***EventOnPlayerRequestRespawn***
***
// If the last defenders request respawn the attackers win
if (ClansNbPlayersAlive[G_DefClan] <= 1
	&& Event.Player != Null
	&& Event.Player.CurrentClan == G_DefClan)
{
	LastDefSelfElim = True;
}
XmlRpc::OnPlayerRequestRespawn(Event);
PassOn(Event);
***

***EventOnCapture***
***
XmlRpc::OnCapture(Event);
PassOn(Event);
***

***EventOnShoot***
***
XmlRpc::OnShoot(Event);
PassOn(Event);
***

***EventOnNearMiss***
***
if (Event.WeaponNum == GetWeaponNum(CSmMode::EWeapon::Laser)) {
	declare MaxMissDist = 0.5;
	if (Event.Shooter != Null && Event.Victim != Null) {
		declare Distance = MathLib::Distance(Event.Shooter.Position, Event.Victim.Position);
		MaxMissDist = Distance * 2 / 100;
		if (MaxMissDist > 0.5) MaxMissDist = 0.5;
	}
	
	if (Event.Shooter != Null && Event.Shooter.CurrentClan == G_AtkClan && Event.MissDist <= MaxMissDist) {
		if (Event.MissDist < 0.01) {
			Message::SendStatusMessage(
				TextLib::Compose(
					_("%1$<%2$> misses $<%3$> by %4cm."), 
					Teams[G_AtkClan - 1].ColorText,
					Event.Shooter.Name, 
					Event.Victim.Name,
					TextLib::ToText(1)
				), 3000, 2
			);
		} else {
			Message::SendStatusMessage(
				TextLib::Compose(
					_("%1$<%2$> misses $<%3$> by %4cm."), 
					Teams[G_AtkClan - 1].ColorText,
					Event.Shooter.Name, 
					Event.Victim.Name,
					TextLib::ToText(MathLib::CeilingInteger(Event.MissDist*100))
				), 3000, 2
			);
		}
		XmlRpc::OnNearMiss(Event);
		PassOn(Event);
	} else {
		Discard(Event);
	}
} else {
	Discard(Event);
}
***

***AllDefElim***
***
AllDefElim = ((ClansNbPlayersAlive[G_DefClan] <= 0 && S_TimePoleElimination <= 0) || LastDefSelfElim);
***

***CreatePlayersListBig***
***
declare ML ="";
	declare PosX = 0.;
	declare PosY = 0.;
	declare Col = 2.;
	declare PlayersByCol = MathLib::NearestInteger(_Nb/Col);
	declare S = _Scale;
	declare K = 1;
	declare AvatarSize = 10.75;
	
	declare RocketHidden = 0;
	if (_Mode == 2) RocketHidden = 1;
	
	ML ^= """
<framemodel posn="0 0" id="Frame_PlayerLeft">
	<quad posn="0 0 2" sizen="{{{3*S}}} 11.5" image="file://Media/Manialinks/ShootMania/Elite/NotReady.dds" id="Quad_NotReady" />
	<quad posn="0 0 2" sizen="{{{3*S}}} 11.5" image="file://Media/Manialinks/ShootMania/Elite/Ready.dds" hidden="1" id="Quad_Ready" />
	<quad posn="{{{0*S}}} 0 1" sizen="{{{81.8*S}}} 11.5" bgcolor="a007" id="Quad_Attacker" hidden="1" />
	<quad posn="{{{0*S}}} 0 5" sizen="{{{81.8*S}}} 11.5" bgcolor="0009" id="Quad_Eliminated" hidden="1" />
	<quad posn="{{{(3*S)+6}}} {{{11.5/-2.}}} 2" sizen="{{{AvatarSize}}} {{{AvatarSize}}}" halign="center" valign="center" bgcolor="0003" opacity="0.9" id="Quad_Avatar" />
	<label posn="{{{17*S}}} -6.4 3" sizen="{{{45*S}}} 12" textsize="3" valign="bottom" id="Label_Name" />
	<label posn="{{{17*S}}} -10.4 3" sizen="{{{24*S}}} 5" textsize="1" valign="bottom" textemboss="1" id="Label_Rank" />
	<frame posn="{{{14.6*S}}} -2.7 4" id="Frame_Stars">
		<quad posn="0 0" sizen="2 2" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_5" hidden="1" />
		<quad posn="0 -1.5" sizen="2 2" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_4" hidden="1" />
		<quad posn="0 -3" sizen="2 2" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_3" hidden="1" />
		<quad posn="0 -4.5" sizen="2 2" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_2" hidden="1" />
		<quad posn="0 -6" sizen="2 2" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_1" hidden="1" />
	</frame>
	<frame posn="{{{63*S}}} -10.4 2" id="Frame_LadderPoints" hidden="1">
		<quad posn="0 -2" sizen="{{{7*S}}} 7" halign="right" valign="bottom" style="Icons128x128_1" substyle="LadderPoints" />
		<label posn="-8 0" sizen="{{{12*S}}} 5" textsize="1" textcolor="bb8" halign="right" valign="bottom" id="Label_LadderPoints" />
	</frame>
	<frame posn="{{{68*S}}} -5.5 2" id="Frame_DefPointsSimple">
		<label posn="0 0" sizen="{{{6*S}}} 3" textsize="2" halign="left" valign="center" id="Label_DefPoints" />
		<quad posn="-0.5 -0.2" sizen="5 5" halign="right" valign="center" image="file://Media/Manialinks/ShootMania/Common/Rocket.dds" hidden="{{{RocketHidden}}}" />
	</frame>
	<frame posn="{{{70*S}}} 0 2" id="Frame_DefPointsDetailed" hidden="1">
		<label posn="0 -1.75" sizen="{{{5*S}}} 3" textsize="2" halign="left" valign="center" id="Label_DefLaser" />
		<label posn="0 -5.25" sizen="{{{5*S}}} 3" textsize="2" halign="left" valign="center" id="Label_DefRocket" />
		<label posn="0 -8.75" sizen="{{{5*S}}} 3" textsize="2" halign="left" valign="center" id="Label_DefNucleus" />
		<quad posn="-0.5 -1.95" sizen="3 3" halign="right" valign="center" image="file://Media/Manialinks/ShootMania/Common/Laser.dds" />
		<quad posn="-0.5 -5.45" sizen="3 3" halign="right" valign="center" image="file://Media/Manialinks/ShootMania/Common/Rocket.dds" />
		<quad posn="-0.5 -8.95" sizen="3 3" halign="right" valign="center" image="file://Media/Manialinks/ShootMania/Common/Nucleus.dds" />
	</frame>
	<label posn="{{{76*S}}} -5.5 3" sizen="{{{5*S}}} 5" textsize="5" halign="left" valign="center" id="Label_AtkPoints" />
</framemodel>
<framemodel posn="0 0" id="Frame_PlayerRight">
	<quad posn="0 0 2" sizen="{{{3*S}}} 11.5" halign="right" image="file://Media/Manialinks/ShootMania/Elite/NotReady.dds" id="Quad_NotReady" />
	<quad posn="0 0 2" sizen="{{{3*S}}} 11.5" halign="right" image="file://Media/Manialinks/ShootMania/Elite/Ready.dds" hidden="1" id="Quad_Ready" />
	<quad posn="{{{0*S}}} 0 1" sizen="{{{81.8*S}}} 11.5" bgcolor="a007" halign="right" id="Quad_Attacker" hidden="1" />
	<quad posn="{{{0*S}}} 0 5" sizen="{{{81.8*S}}} 11.5" bgcolor="0009" halign="right" id="Quad_Eliminated" hidden="1" />
	<quad posn="{{{(-3*S)-6}}} {{{11.5/-2.}}} 2" sizen="{{{AvatarSize}}} {{{AvatarSize}}}" halign="center" valign="center" bgcolor="0003" opacity="0.9" id="Quad_Avatar" />
	<label posn="{{{-17*S}}} -6.4 3" sizen="{{{45*S}}} 12" textsize="3" halign="right" valign="bottom" id="Label_Name" />
	<label posn="{{{-17*S}}} -10.4 3" sizen="{{{24*S}}} 5" textsize="1" halign="right" valign="bottom" textemboss="1" id="Label_Rank" />
	<frame posn="{{{-14.6*S}}} -2.7 4" id="Frame_Stars">
		<quad posn="0 0" sizen="2 2" halign="right" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_5" hidden="1" />
		<quad posn="0 -1.5" sizen="2 2" halign="right" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_4" hidden="1" />
		<quad posn="0 -3" sizen="2 2" halign="right" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_3" hidden="1" />
		<quad posn="0 -4.5" sizen="2 2" halign="right" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_2" hidden="1" />
		<quad posn="0 -6" sizen="2 2" halign="right" valign="center" style="BgRaceScore2" substyle="Fame" id="Quad_Star_1" hidden="1" />
	</frame>
	<frame posn="{{{-63*S}}} -10.4 2" id="Frame_LadderPoints" hidden="1">
		<quad posn="{{{13*S}}} -2" sizen="{{{7*S}}} 7" valign="bottom" style="Icons128x128_1" substyle="LadderPoints" />
		<label posn="{{{12*S}}} 0" sizen="{{{12*S}}} 5" textsize="1" textcolor="bb8" halign="right" valign="bottom" id="Label_LadderPoints" />
	</frame>
	<frame posn="{{{-68*S}}} -5.5 2" id="Frame_DefPointsSimple">
		<label posn="0 0" sizen="{{{6*S}}} 3" textsize="2" halign="right" valign="center" id="Label_DefPoints" />
		<quad posn="0.5 -0.2" sizen="5 5" halign="left" valign="center" image="file://Media/Manialinks/ShootMania/Common/Rocket2.dds" hidden="{{{RocketHidden}}}" />
	</frame>
	<frame posn="{{{-70*S}}} 0 2"id="Frame_DefPointsDetailed" hidden="1">
		<label posn="0 -1.75" sizen="{{{5*S}}} 3" textsize="2" halign="right" valign="center" id="Label_DefLaser" />
		<label posn="0 -5.25" sizen="{{{5*S}}} 3" textsize="2" halign="right" valign="center" id="Label_DefRocket" />
		<label posn="0 -8.75" sizen="{{{5*S}}} 3" textsize="2" halign="right" valign="center" id="Label_DefNucleus" />
		<quad posn="0.5 -1.95" sizen="3 3" halign="left" valign="center" image="file://Media/Manialinks/ShootMania/Common/Laser.dds" />
		<quad posn="0.5 -5.45" sizen="3 3" halign="left" valign="center" image="file://Media/Manialinks/ShootMania/Common/Rocket.dds" />
		<quad posn="0.5 -8.95" sizen="3 3" halign="left" valign="center" image="file://Media/Manialinks/ShootMania/Common/Nucleus.dds" />
	</frame>
	<label posn="{{{-76*S}}} -5.5 3" sizen="{{{5*S}}} 5" textsize="5" halign="right" valign="center" id="Label_AtkPoints" />
</framemodel>
""";
	
	for (J, 1, 2) {
		for (I, 1, PlayersByCol) {
			if (J % 2 == 1) {
				ML ^= """
<frame posn="{{{PosX}}} {{{PosY}}}">
	<quad posn="0 0 -6" sizen="{{{81.8*S}}} 11.5" bgcolor="{{{C_UI_Colors["BG"]}}}" id="Quad_Player_{{{K}}}" scriptevents="1" />
	<frameinstance modelid="Frame_PlayerLeft" id="Frame_Player_{{{K}}}" />
</frame>
""";
			} else {
				ML ^= """
<frame posn="{{{PosX+(164*S/2.)}}} {{{PosY}}}">
	<quad posn="0 0 -6" sizen="{{{81.8*S}}} 11.5" bgcolor="{{{C_UI_Colors["BG"]}}}" halign="right" id="Quad_Player_{{{K}}}" scriptevents="1" />
	<frameinstance modelid="Frame_PlayerRight" id="Frame_Player_{{{K}}}" />
</frame>
""";
			}
			PosY -= 11.9;
			K += 1;
		}
		PosX += 164*S/2.;
		PosY = 0.;
	}
	
	return ML;
***

***CreateLayerScoresTable***
***
return "";

declare CW = 1.;
declare SW = 1.;

declare Team1Color = """{{{Teams[0].ColorPrimary.X}}} {{{Teams[0].ColorPrimary.Y}}} {{{Teams[0].ColorPrimary.Z}}}""";
declare Team2Color = """{{{Teams[1].ColorPrimary.X}}} {{{Teams[1].ColorPrimary.Y}}} {{{Teams[1].ColorPrimary.Z}}}""";

declare ImgPath = "file://Media/Manialinks/ShootMania/Common/";

// Scores table
return """	
<!-- **************** Scores table **************** -->
<frame posn="0 42" id ="Frame_ScoresTable">
	<format textemboss="1"/>
	<quad posn="0 38 -1" sizen="178 140" halign="center" image="{{{ImgPath}}}ScoresBg.dds" />
	<quad posn="-66.3 -1.5 6" sizen="40 36" halign="center" image="{{{ImgPath}}}ScoresBg_left.dds" colorize="{{{Team1Color}}}" />
	<quad posn=" 66.3 -1.5 6" sizen="40 36" halign="center" image="{{{ImgPath}}}ScoresBg_right.dds" colorize="{{{Team2Color}}}" />
	
	<!-- **************** Center panel **************** -->
	<frame posn="0 0">		
		<!-- **************** VS **************** -->
		<frame posn="0 -12" id="Frame_VS">
			<quad posn="-7 -2.5" sizen="15 15" halign="right" valign="bottom" style="Emblems" substyle="#1" />
			<quad posn="7 -2.5" sizen="15 15" valign="bottom" style="Emblems" substyle="#2" />
			<label posn="-46 1.5" sizen="46 5" textsize="4" halign="center" valign="bottom" scale="0.95" opacity="0.88" textemboss="1" id="Label_NameTeam1" />
			<label posn=" 46 1.5" sizen="46 5" textsize="4" halign="center" valign="bottom" scale="0.95" opacity="0.88" textemboss="1" id="Label_NameTeam2" />
		</frame>
		<!-- **************** Legends **************** -->
		<frame posn="0 -17.5">
			<label posn="-9 0" sizen="8 5" textsize="1" halign="right" valign="bottom" text="{{{_("|Substantive|Hit")}}}" />
			<label posn="-2 0" sizen="5 5" textsize="1" halign="right" valign="bottom" text="{{{_("|Attack,Substantive|Atk")}}}" />
			<label posn="2 0" sizen="5 5" textsize="1" valign="bottom" text="{{{_("|Attack,Substantive|Atk")}}}" />
			<label posn="9 0" sizen="8 5" textsize="1" valign="bottom" text="{{{_("|Substantive|Hit")}}}" />
		</frame>
		<!-- **************** Players Ranking **************** -->
		<frame posn="{{{-164*CW/2.}}} -17.7" id="Frame_Ranking">
			{{{CreatePlayersListBig(2, C_RequiredPlayersNb*2, CW)}}}
		</frame>
	</frame>
</frame>
<script><!--
#Include "TextLib" as TL

declare CMlFrame Frame_Ranking;
declare CMlLabel Label_NameTeam1;
declare CMlLabel Label_NameTeam2;

Void UpdateReady(Integer _Slot, Boolean _Ready) {
	declare Frame_Player <=> (Frame_Ranking.GetFirstChild("Frame_Player_"^_Slot) as CMlFrame);
	if (Frame_Player == Null) return;
	declare Quad_NotReady <=> (Frame_Player.GetFirstChild("Quad_NotReady") as CMlQuad);
		
	if (_Ready) Quad_NotReady.ChangeImageUrl("file://Media/Manialinks/ShootMania/Elite/Ready.dds");
	else Quad_NotReady.ChangeImageUrl("file://Media/Manialinks/ShootMania/Elite/NotReady.dds");
}

Void UpdateScores(Integer _Slot, Integer _DefPoints, Integer _AtkPoints) {
	declare Frame_Player <=> (Frame_Ranking.GetFirstChild("Frame_Player_"^_Slot) as CMlFrame);
	if (Frame_Player == Null) return;
	declare Label_DefPoints	<=> (Frame_Player.GetFirstChild("Label_DefPoints") as CMlLabel);
	declare Label_AtkPoints	<=> (Frame_Player.GetFirstChild("Label_AtkPoints") as CMlLabel);
	
	Label_DefPoints.SetText(TL::ToText(_DefPoints));
	Label_AtkPoints.SetText(TL::ToText(_AtkPoints));
}

Void UpdatePlayerStatus(Integer _Slot, Ident _PlayerId) {
	if (!Players.existskey(_PlayerId)) return;
	declare Player <=> Players[_PlayerId];
	
	declare Frame_Player		<=> (Frame_Ranking.GetFirstChild("Frame_Player_"^_Slot)	as CMlFrame);
	if (Frame_Player == Null) return;
	declare Quad_Eliminated		<=> (Frame_Player.GetFirstChild("Quad_Eliminated")		as CMlQuad);
	declare Frame_LadderPoints	<=> (Frame_Player.GetFirstChild("Frame_LadderPoints")	as CMlFrame);
	declare Label_LadderPoints	<=> (Frame_Player.GetFirstChild("Label_LadderPoints")	as CMlLabel);
	declare Quad_Attacker		<=> (Frame_Player.GetFirstChild("Quad_Attacker")		as CMlQuad);
	declare Quad_NotReady		<=> (Frame_Player.GetFirstChild("Quad_NotReady")		as CMlQuad);
	
	if (Player.SpawnStatus == CSmPlayer::ESpawnStatus::Spawned || UI.UISequence != CUIConfig::EUISequence::Playing) {
		Quad_Eliminated.Hide();
	} else {
		Quad_Eliminated.Show();
	}
	
	declare netread Integer Net_LayerST_Sequence for Teams[0];
	if (Net_LayerST_Sequence == {{{C_SequencePodium}}} && Player.Score != Null && Player.Score.LadderScore >= 0.) {
		declare LadderPointsExplode = TL::Split(".", TL::ToText(Player.Score.LadderScore));
		declare LadderPoints = "0.0";
		if (LadderPointsExplode.existskey(0)) LadderPoints = LadderPointsExplode[0];
		if (LadderPointsExplode.existskey(1)) LadderPoints ^= "."^TL::SubString(LadderPointsExplode[1], 0, 2);
		
		Frame_LadderPoints.Show();
		Label_LadderPoints.SetText(LadderPoints);
	} else {
		Frame_LadderPoints.Hide();
	}
	
	declare netread Text Net_LayerST_AttackerLogin for Teams[0];
	if (
		Player.Login == Net_LayerST_AttackerLogin 
		&& Net_LayerST_Sequence == {{{C_SequencePlaying}}} 
		&& UI.UISequence == CUIConfig::EUISequence::Playing
	) {
		Quad_Attacker.Show();
	} else {
		Quad_Attacker.Hide();
	}
	
	if (Net_LayerST_Sequence != {{{C_SequenceWarmUp}}}) {
		Quad_NotReady.Hide();
	} else {
		Quad_NotReady.Show();
	}
}

Void UpdatePlayer(Integer _Slot, Ident _PlayerId) {
	declare Frame_Player <=> (Frame_Ranking.GetFirstChild("Frame_Player_"^_Slot) as CMlFrame);
	if (Frame_Player == Null) return;
	
	declare Quad_Avatar 	<=> (Frame_Player.GetFirstChild("Quad_Avatar")		as CMlQuad);
	declare Label_Name		<=> (Frame_Player.GetFirstChild("Label_Name")		as CMlLabel);
	declare Label_Rank		<=> (Frame_Player.GetFirstChild("Label_Rank")		as CMlLabel);
	declare Frame_Stars		<=> (Frame_Player.GetFirstChild("Frame_Stars")		as CMlFrame);		
	
	if (Players.existskey(_PlayerId)) {
		declare Player <=> Players[_PlayerId];
		
		Frame_Player.Show();
		Quad_Avatar.ChangeImageUrl("file://Avatars/"^Player.Login^"/Default");
		Label_Name.SetText(Player.Name);
		
		declare ZonePath = _("Other");
		if (Player.User.ZonePath != "") {
			declare ExplodeZonePath = TL::Split("|", Player.User.ZonePath);
			if (ExplodeZonePath.existskey(1)) ZonePath = ExplodeZonePath[1];
		}
		declare ZoneRank = "-";
		if (Player.User.LadderRank > 0) ZoneRank = TL::ToText(Player.User.LadderRank);
		Label_Rank.SetText(TL::Compose("%1: %2", ZonePath, ZoneRank));
		
		if (Player.User.FameStars > 0 && Player.User.FameStars <= 5) {
			declare Quad_Star_1 <=> (Frame_Stars.GetFirstChild("Quad_Star_1") as CMlQuad);
			declare Quad_Star_2 <=> (Frame_Stars.GetFirstChild("Quad_Star_2") as CMlQuad);
			declare Quad_Star_3 <=> (Frame_Stars.GetFirstChild("Quad_Star_3") as CMlQuad);
			declare Quad_Star_4 <=> (Frame_Stars.GetFirstChild("Quad_Star_4") as CMlQuad);
			declare Quad_Star_5 <=> (Frame_Stars.GetFirstChild("Quad_Star_5") as CMlQuad);
			Frame_Stars.Show();
			Quad_Star_1.Hide();
			Quad_Star_2.Hide();
			Quad_Star_3.Hide();
			Quad_Star_4.Hide();
			Quad_Star_5.Hide();
			
			if (Player.User.FameStars >= 1) Quad_Star_1.Show();
			if (Player.User.FameStars >= 2) Quad_Star_2.Show();
			if (Player.User.FameStars >= 3) Quad_Star_3.Show();
			if (Player.User.FameStars >= 4) Quad_Star_4.Show();
			if (Player.User.FameStars >= 5) Quad_Star_5.Show();
		} else {
			Frame_Stars.Hide();
		}
		
		declare netread Boolean Net_LayerST_Ready for Player;
		UpdateReady(_Slot, Net_LayerST_Ready);
		
		declare netread Integer Net_LayerST_DefPoints for Player;
		declare netread Integer Net_LayerST_AtkPoints for Player;
		UpdateScores(_Slot, Net_LayerST_DefPoints, Net_LayerST_AtkPoints);
		
		UpdatePlayerStatus(_Slot, _PlayerId);
	} else {
		Frame_Player.Hide();
	}
}

main() {
	Frame_Ranking <=> (Page.GetFirstChild("Frame_Ranking") as CMlFrame);
	Label_NameTeam1 <=> (Page.GetFirstChild("Label_NameTeam1") as CMlLabel);
	Label_NameTeam2 <=> (Page.GetFirstChild("Label_NameTeam2") as CMlLabel);
	for (I, 1, {{{C_RequiredPlayersNb*2}}}) {
		UpdatePlayer(I, NullId);
	}
	
	declare netread Integer			Net_LayerST_Update		for Teams[0];
	declare netread Integer[Text]	Net_LayerST_Logins		for Teams[0];
	declare netread Integer 		Net_LayerST_Sequence	for Teams[0];
	
	declare Initialized = False;
	declare PlayersSlot = Ident[Integer];
	declare PrevUpdate = 0;
	declare PrevUISequence = CUIConfig::EUISequence::None;
	declare PrevSequence = -1;
	declare PrevTeam1Name = "";
	declare PrevTeam2Name = "";
	
	while (True) {
		sleep(100);
		
		if (InputPlayer == Null) continue;
		if (!PageIsVisible) continue;
		
		if (!Initialized) {
			Initialized = True;
			foreach (Player in Players) {
				declare PrevDefPoints for Player = -1;
				declare PrevAtkPoints for Player = -1;
				PrevDefPoints = -1;
				PrevAtkPoints = -1;
			}
		}
		
		if (PrevUpdate != Net_LayerST_Update) {
			PrevUpdate = Net_LayerST_Update;
			
			declare NewPlayersSlot = Ident[Integer];
			foreach (Player in Players) {
				declare Slot for Player = 0;
				if (Net_LayerST_Logins.existskey(Player.Login)) {
					Slot = Net_LayerST_Logins[Player.Login];
					NewPlayersSlot[Slot] = Player.Id;
				} else {
					Slot = 0;
				}
			}
			
			for (I, 1, {{{C_RequiredPlayersNb*2}}}) {
				if (PlayersSlot.existskey(I) && NewPlayersSlot.existskey(I)) {
					if (PlayersSlot[I] != NewPlayersSlot[I]) {
						PlayersSlot[I] = NewPlayersSlot[I];
						UpdatePlayer(I, PlayersSlot[I]);
					}
				} else if (PlayersSlot.existskey(I)) {
					declare Removed = PlayersSlot.removekey(I);
					UpdatePlayer(I, NullId);
				} else if (NewPlayersSlot.existskey(I)) {
					PlayersSlot[I] = NewPlayersSlot[I];
					UpdatePlayer(I, PlayersSlot[I]);
				} 
			}
		}
		
		if (PrevTeam1Name != Teams[0].ColorizedName || PrevTeam2Name != Teams[1].ColorizedName) {
			PrevTeam1Name = Teams[0].ColorizedName;
			PrevTeam2Name = Teams[1].ColorizedName;
			
			if (Teams[0].ColorizedName == "$<$00fBlue$>") Label_NameTeam1.SetText("$<$fffBlue$>");
			else Label_NameTeam1.SetText(Teams[0].ColorizedName);
			if (Teams[1].ColorizedName == "$<$f00Red$>") Label_NameTeam2.SetText("$<$fffRed$>");
			else Label_NameTeam2.SetText(Teams[1].ColorizedName);
		}
		
		foreach (Player in Players) {
			declare Slot for Player = 0;
			if (Slot <= 0) continue;
			
			declare netread Boolean Net_LayerST_Ready for Player;
			declare PrevReady for Player = False;
			
			if (PrevReady != Net_LayerST_Ready) {
				PrevReady = Net_LayerST_Ready;
				UpdateReady(Slot, PrevReady);
			}
			
			declare netread Integer Net_LayerST_DefPoints for Player;
			declare netread Integer Net_LayerST_AtkPoints for Player;
			declare PrevDefPoints for Player = -1;
			declare PrevAtkPoints for Player = -1;
			if (PrevDefPoints != Net_LayerST_DefPoints || PrevAtkPoints != Net_LayerST_AtkPoints) {
				PrevDefPoints = Net_LayerST_DefPoints;
				PrevAtkPoints = Net_LayerST_AtkPoints;
				UpdateScores(Slot, PrevDefPoints, PrevAtkPoints);
			}
			
			declare PrevSpawnStatus for Player = CSmPlayer::ESpawnStatus::Spawned;
			if (PrevSpawnStatus != Player.SpawnStatus) {
				PrevSpawnStatus = Player.SpawnStatus;
				UpdatePlayerStatus(Slot, Player.Id);
			}
		}
		
		if (PrevUISequence != UI.UISequence || PrevSequence != Net_LayerST_Sequence) {
			PrevUISequence = UI.UISequence;
			PrevSequence = Net_LayerST_Sequence;
			
			for (I, 1, {{{C_RequiredPlayersNb*2}}}) {
				if (PlayersSlot.existskey(I)) UpdatePlayerStatus(I, PlayersSlot[I]);
			}
		}
		
	}
}
--></script>
""";
***


***UpdateLayerScoresTable***
***
declare Logins = Integer[Text];
declare AttackerLogin = "";
declare ClansOrders = [1 => WarmUp2::GetGroup("Clan1"), 2 => WarmUp2::GetGroup("Clan2")];
for (I, 1, 2) {
	foreach (Slot => Id in ClansOrders[I]) {
		if (Players.existskey(Id)) {
			declare Player <=> Players[Id];
			Logins[Player.Login] = Slot + ((I-1) * C_RequiredPlayersNb);
			if (Player.Id == G_AtkPlayerId) AttackerLogin = Player.Login;
			
			declare netwrite Net_LayerST_Ready for Player = False;
			Net_LayerST_Ready = WarmUp2::IsReady(Player);
		}
	}
}

declare netwrite Net_LayerST_Update			for Teams[0] = -1;
declare netwrite Net_LayerST_Logins			for Teams[0] = Integer[Text];
declare netwrite Net_LayerST_AttackerLogin	for Teams[0] = "";
declare netwrite Net_LayerST_Sequence		for Teams[0] = _Mode;
Net_LayerST_Update = Now;
Net_LayerST_Logins = Logins;
Net_LayerST_AttackerLogin = AttackerLogin;
Net_LayerST_Sequence = _Mode;
***

***StartTurn***
***
Interface::CreatePlayersLists("Small", 1., 10, 1);
***

***EndTurnUIStart***
***
Interface::DestroyPlayersLists();
***

***StartWarmUp***
***
Interface::CreatePlayersLists("Medium", 0.67, 10, 1);

WarmUp2::SetAllSlotsIcons("Clan1", "file://Media/Manialinks/ShootMania/Common/Laser.dds");
WarmUp2::SetAllSlotsIcons("Clan2", "file://Media/Manialinks/ShootMania/Common/Laser.dds");
WarmUp2::SetSlotIcon("Clan1", G_AtkSlot[1], "file://Media/Manialinks/ShootMania/Common/Nucleus.dds");
WarmUp2::SetSlotIcon("Clan2", G_AtkSlot[2], "file://Media/Manialinks/ShootMania/Common/Nucleus.dds");
***

***EndWarmUp***
***
Interface::DestroyPlayersLists();
***

***FindMaster***
***
declare MasterLogin = "";
***

***LadderPoints***
***
foreach (Score in Scores) {
	declare PrevPoints for Score = 0;
	declare NbHit for Score = 0;
	PrevPoints = Score.Points;
	Score.LadderMatchScoreValue = (Score.Points * 1.) + (NbHit * 0.3);
	// Winner
	if (Score.LadderClan == WinnerMapClan) {
		Score.LadderMatchScoreValue *= 2.;
		Score.Points = 3;
	}
	// Looser
	else if (Score.LadderClan == 3 - WinnerMapClan) {
		Score.LadderMatchScoreValue *= 1.;
		Score.Points = 2;
	}
	// Other
	else {
		Score.LadderMatchScoreValue *= 0.5;
		Score.Points = 1;
	}
}
Ladder_SetResultsVersion(1);
Score::MatchEnd(True);
foreach (Score in Scores) {
	declare PrevPoints for Score = 0;
	Score.Points = PrevPoints;
}
***

/* ------------------------------------- */
/// Update the Hud3dMarkers.
Void UpdateMarkers() {
	// Set the marker above the goal
	UIManager.UIAll.MarkersXML = "";
	
	declare AtkPlayerLogin = "";
	if (Players.existskey(G_AtkPlayerId)) AtkPlayerLogin = Players[G_AtkPlayerId].Login;
	
	declare ColorPoles = Teams[G_DefClan - 1].ColorText;
	declare ColorNucleus = TextLib::SubString(Teams[G_AtkClan - 1].ColorText, 1, 3);
	
	declare NucleusMarker = "";
	if (AtkPlayerLogin != "") {
		NucleusMarker = """<marker icon="Player3" playerlogin="{{{AtkPlayerLogin}}}" box="0 2 0" />""";
	}
	
	declare PolesMarker = "";
	foreach (LandmarkGauge in MapLandmarks_Gauge) {
		declare Icon = "";
		declare Name = "";
		if (LandmarkGauge.Tag == "Goal A") { Icon = "PointA"; Name = "$w$s$oA"; }
		else if (LandmarkGauge.Tag == "Goal B") { Icon = "PointB"; Name = "$w$s$oB"; }
		else if (LandmarkGauge.Tag == "Goal C") { Icon = "PointC"; Name = "$w$s$oC"; }
		else continue;
		PolesMarker ^= """<marker icon="{{{Icon}}}" pos="{{{LandmarkGauge.Position.X}}} {{{LandmarkGauge.Position.Y + 25}}} {{{LandmarkGauge.Position.Z}}}" />""";
	}
		
	foreach (Player in Players) {
		declare UI <=> UIManager.GetUI(Player);
		if (UI == Null) continue;
		
		if (Player.CurrentClan == G_AtkClan && Player.Login != AtkPlayerLogin) {
			UI.MarkersXML = NucleusMarker ^ PolesMarker;
		} else {
			UI.MarkersXML = PolesMarker;
		}
	}
	foreach (Spectator in Spectators) {
		declare UI <=> UIManager.GetUI(Spectator);
		if (UI == Null) continue;
		UI.MarkersXML = PolesMarker ^ NucleusMarker;
	}
}